#+TITLE: doom-literate-config
#+AUTHOR: Łukasz Gruner
#+EMAIL: lukasz@gruner.lu
#+DESCRIPTION: Doom Emacs literate config.
#+TAGS: package(p) advice(a) disabled(d) hook(h) bind(b) setq(s) popup(u) hack(H) defines(D) env(e) command(c)
#+PROPERTY: header-args:emacs-lisp :tangle ~/.doom.d/config.el :tangle-mode (identity #o400) :results silent :exports code :lexical yes
#+TODO: TODO
#+startup: indent fold

* =*DOOM*=
** Add file headers
*** for =config.el=
#+begin_src emacs-lisp
;;; ~/.doom.d/config.el -*- lexical-binding: t; -*-
#+end_src
*** and =packages.el=
#+begin_src emacs-lisp :tangle ~/.doom.d/packages.el
;;; ~/.doom.d/packages.el -*- no-byte-compile: t; lexical-binding: t; -*-
#+end_src
*** and =custom.el=                                                     :setq:
#+begin_src emacs-lisp
(setq! custom-file (concat doom-user-dir "custom.el"))
(unless (file-exists-p custom-file)
  (with-temp-file custom-file
    (insert ";;; ~/.doom.d/custom.el -*- no-byte-compile: t; lexical-binding: t; -*-\n")))
#+end_src
** =MODULES=
#+begin_src emacs-lisp :tangle ~/.doom.d/init.el
;;; ~/.doom.d/init.el -*- no-byte-compile: t; lexical-binding: t; -*-
(doom! :completion (company -tng -childframe) (vertico +icons)
       :ui         doom hl-todo (modeline +light)
                   nav-flash ophints (treemacs +lsp) unicode
                   (popup +all +defaults) (vc-gutter +pretty) vi-tilde-fringe
                   (window-select +numbers +switch-window)
       :editor     (evil +everywhere) file-templates fold
                   format snippets word-wrap
       :emacs      (dired +icons) electric (ibuffer +icons) undo vc
       :term       vterm
       :checkers   syntax
       :tools      (ansible +lsp) (debugger +lsp) (docker +lsp)
                   editorconfig (eval +overlay)
                   lookup (lsp +peek) magit make
                   taskrunner terraform tree-sitter
       :os         tty
       :lang       (cc +lsp) data emacs-lisp (go +lsp)
                   (java +lsp) (javascript +lsp) (json -lsp)
                   lua markdown (org +hugo) (python +lsp) rest
                   (sh +fish -lsp) (web +lsp) (yaml -lsp)
       :config     literate (default +bindings +smartparens))
#+end_src
** My Library of useful things
*** ~uki/local-remap-q-kills-buffer-h~                                  :defines:
Some modes don't require macros (like most RO buffers).
#+begin_src emacs-lisp
(defun uki/local-remap-q-kills-buffer-h ()
  "Locally remap <q> to `quit-window'."
  (define-key evil-normal-state-local-map (kbd "q") #'quit-window)
  (define-key evil-visual-state-local-map (kbd "q") #'quit-window))
#+end_src
*** ~uki/wsl-paste~                                                     :defines:
#+begin_src emacs-lisp
(defun uki/wsl-paste ()
  "Paste using Windows' clipboard."
  (interactive)
  (save-excursion
    (let ((text (shell-command-to-string "powershell.exe -command 'Get-Clipboard' | dos2unix")))
      ;; powershell adds an extra newline
      (setq text (substring text 0 -1))
      (insert-for-yank text))))
#+end_src
* Minor-modes
** Builtin, doom and one-liners
*** Personal info                                                       :setq:
#+begin_src emacs-lisp
(setq! user-full-name    "Łukasz Gruner"
       user-mail-address "lukasz@gruner.lu")
#+end_src
*** Set theme & font                                                    :setq:
~doom-zenburn-theme~ is installed by [[My =MODULES= (init.el)][=:ui doom=]]
#+begin_src emacs-lisp
(let ((fn (cond
           ((find-font (font-spec :name "Terminus (TTF)")) "Terminus (TTF)")
           ((find-font (font-spec :name "Terminus")) "Terminus")
           (t "Monospace"))))
  (setq! doom-theme               'doom-zenburn
         doom-variable-pitch-font "Noto Sans:size=10:antialias=off"
         doom-font                (concat fn ":size=16:antialias=off")
         doom-big-font            (concat fn ":size=24:antialias=off")))
#+end_src
*** Start maximized                                                     :setq:
#+begin_src emacs-lisp
(add-to-list 'default-frame-alist '(fullscreen . maximized))
(add-to-list 'initial-frame-alist '(fullscreen . maximized))
#+end_src
*** Define const vars                                                   :defines:
#+begin_src emacs-lisp
(defgroup uki nil "Group for my things." :group 'users :prefix "uki/")
(defconst uki/dotfiles-dir (expand-file-name "~/src/dot/") "Dotfiles repository root.")
(defconst uki/ellipsis "…" "Ellipsis.")
#+end_src
*** Make scripts executable on save                                     :hook:
#+begin_src emacs-lisp
(add-hook! 'after-save-hook #'executable-make-buffer-file-executable-if-script-p)
#+end_src
*** *<mouse-1>* should follow links                                     :setq:
#+begin_src emacs-lisp
(setq! mouse-1-click-follows-link t)
#+end_src
*** Enable ~+global-word-wrap-mode~
#+begin_src emacs-lisp
(+global-word-wrap-mode 1)
#+end_src
*** Whitespace                                                          :hook:setq:
#+begin_src emacs-lisp
(setq-hook! '(prog-mode-hook org-mode-hook)
  whitespace-style '(face trailing)
  show-trailing-whitespace t)
#+end_src
*** Enable native compilation of packages
#+begin_src emacs-lisp
(setq! package-native-compile t)
#+end_src
*** Start ~doom/open-scratch-buffer~ in last buffer's ~major-mode~
#+begin_src emacs-lisp
(unless noninteractive
  (setq! doom-scratch-initial-major-mode t))
#+end_src
** Consult
*** Buffer list filtering
#+begin_src emacs-lisp
(after! consult
  (setq! consult-buffer-filter
         `(,@consult-buffer-filter
           "\\`\\*scratch\\*\\'"
           "\\`\\*Messages\\*\\'"
           "\\`\\*helpful"
           "\\`\\*doom\\*\\'"
           "\\`\\*\\(?:eslint\\)\\(?:::stderr\\)?\\*\\'"
           "\\`\\*\\(?:emmet-\\|html-\\|css-\\|xml\\)\\ls\\(?:::stderr\\)?\\*\\'"
           "\\`magit")))
#+end_src
*** Next/prev buffer switching will skip visible buffers
#+begin_src emacs-lisp
(after! window
  (setq! switch-to-prev-buffer-skip t))
#+end_src
** Hl-Todo
*** Highlight background                                                :setq:
#+begin_src emacs-lisp
(after! hl-todo
  (setq! hl-todo-color-background t))
#+end_src
** Evil
*** Always use windows <C-w> bindings                                   :setq:
#+begin_src emacs-lisp
(after! evil
  (setq! evil-want-C-w-in-emacs-state t))
#+end_src
*** Also use arrows for switching windows                               :bind:
#+begin_src emacs-lisp
(map! :after evil :map evil-window-map
      "<left>"  #'evil-window-left
      "<right>" #'evil-window-right
      "<up>"    #'evil-window-up
      "<down>"  #'evil-window-down)
#+end_src
*** Enable =vimish-fold-mode= globally                                  :hook:defines:
:PROPERTIES:
:CREATED:  [2023-01-20 16:33]
:END:
#+begin_src emacs-lisp
(add-hook! 'after-init-hook
  (defun uki/enable-vimish-fold-mode-h ()
    (global-evil-vimish-fold-mode 1)))
#+end_src
** Flycheck
*** Always disable textlint                                             :hook:defines:
#+begin_src emacs-lisp
(after! flycheck
  (add-hook! '(org-mode-hook text-mode-hook prog-mode-hook)
    (defun uki/flycheck-disable-textlint-h ()
      (require 'flycheck)
      (cl-pushnew 'textlint flycheck-disabled-checkers))))
#+end_src
** LSP
*** Settings                                                            :setq:
#+begin_src emacs-lisp
(after! lsp
  (require 'lsp-ui)
  (setq! lsp-enable-symbol-highlighting nil
         lsp-ui-doc-enable t
         lsp-ui-doc-show-with-mouse nil
         lsp-ui-doc-show-with-cursor nil
         lsp-lens-enable t
         lsp-headerline-breadcrumb-enable nil
         lsp-ui-sideline-enable t
         lsp-ui-sidelin-show-code-actions t
         lsp-ui-sideline-show-hover t))
#+end_src
** Treemacs
:PROPERTIES:
:CREATED:  [2022-08-19 20:57]
:END:
*** Sync with LSP workspace                                             :hook:defines:
#+begin_src emacs-lisp
(after! treemacs
    (lsp-treemacs-sync-mode 1))
#+end_src
*** Highlight current file
#+begin_src emacs-lisp
(after! treemacs
  (treemacs-follow-mode 1))
#+end_src
*** Git mode extended
:PROPERTIES:
:CREATED:  [2023-01-27 18:59]
:END:
#+begin_src emacs-lisp
(setq! +treemacs-git-mode 'deferred)
#+end_src
** Keychain
:PROPERTIES:
:CREATED:  [2022-08-22 11:14]
:END:
*** Package                                                             :package:
:PROPERTIES:
:CREATED:  [2022-08-22 11:14]
:END:
#+begin_src emacs-lisp :tangle ~/.doom.d/packages.el
(package! keychain-environment)
#+end_src
*** Autostart                                                           :hook:
:PROPERTIES:
:CREATED:  [2022-08-22 11:17]
:END:
#+begin_src emacs-lisp
(add-hook! 'after-init-hook #'keychain-refresh-environment)
#+end_src
** Pinentry
:PROPERTIES:
:CREATED:  [2022-08-22 11:31]
:END:
*** Package                                                             :package:
:PROPERTIES:
:CREATED:  [2022-08-22 11:31]
:END:
#+begin_src emacs-lisp :tangle ~/.doom.d/packages.el
(package! pinentry)
#+end_src
*** Autostart
:PROPERTIES:
:CREATED:  [2022-08-22 11:40]
:END:
#+begin_src emacs-lisp
(add-hook! 'after-init-hook #'pinentry-start)
(add-hook! 'kill-emacs-hook #'pinentry-stop)
#+end_src
*** Enable gpg-sign for git commits                                     :setq:
#+begin_src emacs-lisp
(after! epg
  (setq! epg-pinentry-mode 'loopback))
#+end_src
** Display-Line-Numbers
:PROPERTIES:
:CREATED:  [2023-01-25 21:07]
:END:
But only in prog & conf mode.
#+begin_src emacs-lisp
(after! display-line-numbers
  (remove-hook! 'text-mode-hook #'display-line-numbers-mode))
#+end_src
** Eldoc
*** TODO Lsp co-op
#+begin_src emacs-lisp
(defun uki/enable-eldoc-for-lsp-h ()
    "Configure and enable `eldoc-mode' using `lsp-mode' features."
    (interactive)
    (require 'eldoc nil t)
    (add-hook 'eldoc-documentation-functions #'lsp-eldoc-function -90 t)
    (eldoc-mode 1))
#+end_src
** Desktop-save
#+begin_src emacs-lisp
(add-hook! 'after-init-hook
  (defun uki/enable-desktop-save-mode-h (&rest _)
    (desktop-save-mode 1)))
#+end_src
* Major-modes
** Magit
*** Never use git wrapper                                               :setq:
#+begin_src emacs-lisp
(after! magit
  (setq! magit-git-executable (file-truename "/usr/bin/git")))
#+end_src
*** Hide magit buffers from lists                                       :setq:
#+begin_src emacs-lisp :tangle no
(after! magit-mode
  (setq! magit-buffer-name-format "%M%v: %t"))

(set-popup-rule! "^\\(?: magit\\)" :ignore t)
#+end_src
** Messages
*** <q> emacs-buffer/window                                             :hook:
Messages buffer already exists so hook won't get called until it gets restarted. We need to modify bindings manually.
#+begin_src emacs-lisp
(add-hook! 'messages-buffer-mode-hook #'uki/local-remap-q-kills-buffer-h)
(with-current-buffer "*Messages*" (uki/local-remap-q-kills-buffer-h))
#+end_src
** Prog
*** <C-x =>/<SPC c => Align your code in a pretty way                   :bind:
#+begin_src emacs-lisp
(map! :map prog-mode-map
      "C-x =" #'align-regexp
      (:leader :prefix ("c" "+code")
        :desc "Align regexp" "=" #'align-regexp))
#+end_src
** Dired
*** Reuse current dired buffer when changing directories                :bind:
#+begin_src emacs-lisp
(defun uki/dired-up-directory ()
  "Use single instance of dired buffer when going up a directory."
  (interactive)
  (set-buffer-modified-p nil) ;; don't need to save dired buffers
  (find-alternate-file ".."))

(defun uki/dired-find-file ()
  "Use single instance of dired buffer when opening files."
  (interactive)
  (let ((file (dired-get-file-for-visit)))
    (if (file-directory-p file)
        (progn
          (set-buffer-modified-p nil) ;; don't need to save dired buffers
          (find-alternate-file file))
      (find-file file))))

(map! :after dired :map dired-mode-map
      [remap dired-find-file]    #'uki/dired-find-file
      [remap dired-up-directory] #'uki/dired-up-directory)
#+end_src
*** Permanent ~dired-hide-details-mode~                                 :bind:
press <space m d> to toggle
#+begin_src emacs-lisp
(defun uki/permanent-dired-hide-details-mode-set (sym exp)
  "Restore saved mode state or set a new value."
  (custom-initialize-reset sym exp)
  (when (eq major-mode 'dired-mode) (dired-hide-details-mode exp))
  (if exp
      (add-hook! 'dired-mode-hook #'dired-hide-details-mode)
    (remove-hook! 'dired-mode-hook #'dired-hide-details-mode)))

(defcustom uki/permanent-dired-hide-details-mode-state nil
  "State of `dired-hide-details-mode' saved between restarts."
  :group 'uki
  :type 'bool
  :initialize #'uki/permanent-dired-hide-details-mode-set)

(defun uki/permanent-dired-hide-details-mode-toggle ()
  "Toggles `dired-hide-details-mode' for current and future dired buffers."
  (interactive)
  (uki/permanent-dired-hide-details-mode-set
   'uki/permanent-dired-hide-details-mode-state
   (not uki/permanent-dired-hide-details-mode-state)))

(map! :after dired :map dired-mode-map
      (:localleader :desc "dired-hide-details (toggle)" "d" #'uki/permanent-dired-hide-details-mode-toggle)
      ([remap dired-hide-details-mode] #'uki/permanent-dired-hide-details-mode-toggle))
#+end_src
*** Run dired instead of listing directory
Why so complicated? [[https://nullprogram.com/blog/2019/12/10/#cl-first]]
#+begin_src emacs-lisp
(after! dired
  (defalias 'list-directory 'dired)
  (put 'list-directory 'byte-optimizer 'byte-compile-inline-expand))
#+end_src
** Org
*** =LIBRARY=
**** ~uki/org-heading-level~
#+begin_src emacs-lisp
(defun uki/org-heading-level ()
  "Returns level of org headline at point"
  (car-safe (org-heading-components)))
#+end_src
**** ~uki/org-has-property~
#+begin_src emacs-lisp
(defun uki/org-has-property (property &optional pos inherit)
  "If current org entry (under point or POS) has prop PROPERTY."
  (not (null (org-entry-get (or pos (point)) property inherit))))
#+end_src
*** Startup options                                                     :setq:
#+begin_src emacs-lisp
(after! org
  (setq! org-tags-column 80
         org-startup-indented t
         org-startup-folded t
         org-startup-truncated t
         org-startup-align-all-tables t))
#+end_src
*** My ~org-directory~                                                  :setq:
#+begin_src emacs-lisp
(setq! org-directory (file-truename "~/mnt/OneDrive/doc/"))
#+end_src
*** Bind ~org-babel-tangle~ under :localleader                          :bind:
#+begin_src emacs-lisp
(map! :after org :map org-mode-map
      :localleader :desc "Tangle current file" "B" #'org-babel-tangle)
#+end_src
*** Follow link under point with <RET>                                  :setq:
#+begin_src emacs-lisp
(after! org
  (setq! org-return-follows-link t))
#+end_src
*** Have export dialog open in place and without modeline               :popup:
#+begin_src emacs-lisp
(after! org
  (set-popup-rule! "^ ?\\*Org Export Dispatcher"
    :actions '(display-buffer-same-window)
    :side nil
    :width nil
    :height nil
    :size nil
    :quit nil
    :select t
    :modeline nil))
#+end_src
*** Have =org-src= buffers be managed by orgmode                        :setq:popup:
#+begin_src emacs-lisp
(after! org
  (setq! org-src-window-setup 'current-window)

  (set-popup-rule! "^\\*Org Src" :ignore t))
#+end_src
*** Enable eldoc-mode for src blocks                                    :hook:
#+begin_src emacs-lisp
(after! org
  (add-hook! 'org-mode-hook
    (defun uki/enable-eldoc-h ()
      "Setup `eldoc-documentation-functions' for elisp."
      (interactive)
      (add-hook! 'eldoc-documentation-functions :local #'elisp-eldoc-funcall)
      (add-hook! 'eldoc-documentation-functions :local #'elisp-eldoc-var-docstring)
      (eldoc-mode 1))))
#+end_src
*** Add Elisp src block template                                        :setq:
#+begin_src emacs-lisp
(after! org
  (require 'org-tempo)
  (add-to-list 'org-structure-template-alist '("el" . "src emacs-lisp")))
#+end_src
*** Hugo
**** Add Hugo properties                                                     :bind:defines:
#+Begin_src emacs-lisp
(defcustom uki/default-export-file-name-for-hugo "slug"
  "Default value of EXPORT_FILE_NAME property for ox-hugo."
  :group 'uki
  :type 'string)

(defun uki/org-hugo-get-point-of-export-heading (&optional property)
  "Returns point value at org headline when headline or it's sibilings
containin property \"EXPORT_FILE_NAME\" or PROPERTY."
  (unless property
    (setq property "EXPORT_FILE_NAME"))
  (save-excursion
    (unless (org-at-heading-p)
      (org-back-to-heading))
    (while (not (or (uki/org-has-property property)
                    (eq 1 (uki/org-heading-level))
                    (seq-contains-p ;;check if sibilings in curr tree have the property
                     (org-map-entries (lambda () (uki/org-has-property property))
                                      (format "LEVEL=%d" (uki/org-heading-level))
                                      'tree)
                     t)))
      (org-up-heading-all 1))
    (point)))

(defun uki/org-hugo-set-properties ()
  "Sets properties required by Hugo."
  (interactive)
  (save-excursion
    (let* ((ed "EXPORT_DATE")
           (prop "EXPORT_FILE_NAME")
           (default (or (org-entry-get (point) prop t)
                        uki/default-export-file-name-for-hugo))
           (slug (if (called-interactively-p 'any)
                     (read-string
                      "Enter value for property `EXPORT_FILE_NAME': "
                      default)
                   default)))
      (unless (uki/org-has-property prop (point) nil)
        (goto-char (uki/org-hugo-get-point-of-export-heading)))
      (org-set-property prop slug)
      (org-set-property (if (uki/org-has-property ed) "EXPORT_HUGO_LASTMOD" ed)
                        (format-time-string org-hugo-date-format)))))

(map! :after org :map org-mode-map
      :localleader :desc "Set HUGO properties" "H" #'uki/org-hugo-set-properties)
#+end_src
**** Use YAML format for generated front matter                        :setq:
#+begin_src emacs-lisp
(after! ox-hugo
  (setq! org-hugo-front-matter-format "yaml"))
#+end_src
**** Have emacs do the code-higlighting                                :advice:setq:defines:
#+begin_src emacs-lisp
(after! ox-html
  (setq! org-html-htmlize-output-type 'css
         org-html-wrap-src-lines t))

(defcustom uki/hugo-code-hilight-use-html t
  "If t, use emacs syntax highlighting. If nil let Hugo do it's job."
  :group 'uki
  :type 'boolean)

(defun uki/hugo-toggle-code-html ()
  "Toggles `uki/hugo-code-hilight-use-html'."
  (interactive)
  (setq! uki/hugo-code-hilight-use-html (not uki/hugo-code-hilight-use-html))
  (message "Hugo will %suse html for code."
           (if uki/hugo-code-hilight-use-html "" "not ")))

(defun uki/hugo-code-highlight-a (fn &rest args)
    "Render HTML, with highlighted code syntax."
    (require 'ox-html)
    (if uki/hugo-code-hilight-use-html
        (apply #'org-html-src-block args)
      (apply fn args)))

(after! ox-hugo
  (advice-add #'org-hugo-src-block :around #'uki/hugo-code-highlight-a))
#+end_src
** Info
*** Automatically view '.info' files instead of editing them            :setq:
#+begin_src emacs-lisp
(add-to-list 'auto-mode-alist
             (cons "\\.[iI][nN][fF][oO]\\'"
                   (defun uki/reopen-file-in-info-mode ()
                     "Re-run this in an info viewer."
                     (interactive)
                     (let ((file-name (buffer-file-name)))
                       (kill-buffer)
                       (info file-name)))))
#+end_src
*** Display =*info*= buffers in same window                            :popup:
#+begin_src emacs-lisp
(set-popup-rule! "^\\*info\\*$"
  :actions '(display-buffer-same-window)
  :modeline t)
#+end_src
*** Left mouse button should follow links                               :bind:
#+begin_src emacs-lisp
(map! :after info :map Info-mode-map
      "<mouse-1>" #'Info-mouse-follow-nearest-node)
#+end_src
** Emacs-Lisp
*** [Flycheck] Disable elisp-checkdoc                                   :hook:setq:
#+begin_src emacs-lisp
(add-hook! 'emacs-lisp-mode-hook
  (defun uki/flycheck-disable-checkdoc-h ()
    (require 'flycheck)
    (cl-pushnew 'emacs-lisp-checkdoc flycheck-disabled-checkers)))
#+end_src
** Java
*** Use long lines                                                      :hook:setq:
#+begin_src emacs-lisp
(setq-hook! 'java-mode-hook
  fill-column 180)
#+end_src
*** Load formatter configuration if present                             :hook:setq:defines:
#+begin_src emacs-lisp
(defun uki/java-setup-formatter-h (&rest _)
  "Setup lsp java formatter. Reads $FORMATTER_PROFILE for settings profile."
  (interactive)
  (require 'lsp-java nil t)
  (let ((formatter (file-truename "~/.formatter.xml"))
        (profile (or (getenv "FORMATTER_PROFILE") "default")))
    (if (file-exists-p formatter)
        (setq lsp-java-format-enabled t
              lsp-java-format-settings-profile profile
              lsp-java-format-settings-url formatter)
      (message "No formatter file present: %s" formatter))))

(add-hook! 'java-mode-hook #'uki/java-setup-formatter-h)
#+end_src
*** LSP Settings                                                        :setq:
#+begin_src emacs-lisp
(after! lsp-java
  (setq! lsp-java-java-path "/usr/bin/java"
         lsp-java-dependency-package-representation "hierarchical"
         lsp-java-maven-download-sources t
         lsp-java-references-code-lens-enabled t
         lsp-java-implementations-code-lens-enabled t
         lsp-java-configuration-maven-user-settings (file-truename "~/.m2/settings.xml")
         lsp-java-code-generation-use-blocks t
         lsp-java-save-actions-organize-imports t))
#+end_src
*** Enable eldoc
#+begin_src emacs-lisp
(add-hook! 'java-mode-hook #'uki/enable-eldoc-for-lsp-h)
#+end_src
** Compilation
*** Have <q> exit window                                           :hook:bind:
#+begin_src emacs-lisp
(add-hook! 'compilation-mode-hook #'uki/local-remap-q-kills-buffer-h)
#+end_src
** Special
*** Have <q> exit window                                           :hook:bind:
#+begin_src emacs-lisp
(add-hook! 'special-mode-hook #'uki/local-remap-q-kills-buffer-h)
#+end_src
** Comint
*** Have <q> exit window                                           :hook:bind:
#+begin_src emacs-lisp
(add-hook! 'comint-mode-hook #'uki/local-remap-q-kills-buffer-h)
#+end_src
** Powershell
*** Set lsp exe location                                                :setq:
TODO: fix: M-x lsp-install-server
#+begin_src emacs-lisp
(after! lsp-pwsh
  (setq! lsp-pwsh-exe (executable-find "powershell.exe")))
#+end_src
** Vterm
*** Disable solaire mode                                                :hook:
When enabled, vterm background is too dark to see dark-blue (filename) color.
#+begin_src emacs-lisp
(add-hook! 'vterm-mode-hook #'turn-off-solaire-mode)
#+end_src
* Custom
** Load
#+begin_src emacs-lisp
(load custom-file)
#+end_src
** ask about unsaved customizations
#+begin_src emacs-lisp
(add-hook! 'kill-emacs-query-functions #'custom-save-all)
#+end_src
* Auto tangle & compile
My =~/.doom.d/config.org= is a softlink, so when you edit the file ~+literate-enable-recompile-h~ will not recognize it as literate config.

As a workaround I disable literate autotangle.
#+begin_src emacs-lisp
(after! org
  (remove-hook 'org-mode-hook #'+literate-enable-recompile-h))
#+end_src

And then add following at the end of =config.org= (this will also compile tangled files).
: # Local Variables:
: # eval: (add-hook! 'after-save-hook :local (progn (org-babel-tangle) (byte-recompile-directory doom-user-dir 0 t)))
: # End:
